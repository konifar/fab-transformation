buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
    }
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'konifar'
version = '1.0.0'
def siteUrl = 'https://github.com/konifar/fab-transformation'
def gitUrl = 'https://github.com/konifar/fab-transformation.git'

bintray {
    user = ''
    key = ''

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'fab-transformation'
        desc = 'Support Floating Action Button transformation for Android.'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/konifar/fab-transformation/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['android']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'fab-transformation'
                url siteUrl
                artifactId 'fab-transformation'
                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id 'konifar'
                        name 'Yusuke Konishi'
                        email 'yahpeycoy0403@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/konifar/fab-transformation.git'
                    developerConnection 'https://github.com/konifar/fab-transformation.git'
                    url siteUrl
                }
            }
        }
    }
}

dependencies {
    compile 'com.nineoldandroids:library:2.4.0+'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}